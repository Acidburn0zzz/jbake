apply plugin: 'maven'
apply plugin: 'signing'

def jbakePom = pom {
    project {
        description 'JBake is a Java based open source static site/blog generator for developers.'
        url 'http://jbake.org'
        developers {
            developer {
                id 'jonbullock'
                name 'Jonathan Bullock'
                email 'jonbullock@gmail.com'
                url 'http://jonathanbullock.com'
                timezone '0'
            }
        }
        scm {
            url 'https://github.com/jbake-org/jbake/'
            connection 'scm:git:git@github.com:jbake-org/jbake.git'
            developerConnection 'scm:git:https://github.com/jbake-org/jbake.git'
        }
        issueManagement {
            system 'GitHub Issues'
            url 'https://github.com/jbake-org/jbake/issues'
        }
        mailingLists {

            mailingList {
                name 'jbake-dev'
                subscribe 'jbake-dev@googlegroups.com'
                unsubscribe 'jbake-dev+unsubscribe@googlegroups.com'
                archive 'http://groups.google.com/group/jbake-dev'
            }
            mailingList {
                name 'jbake-user'
                subscribe 'jbake-user@googlegroups.com'
                unsubscribe 'jbake-user+unsubscribe@googlegroups.com'
                archive 'http://groups.google.com/group/jbake-user'
            }
        }

        licenses {
            license {
                name 'The MIT License (MIT)'
                url 'http://opensource.org/licenses/MIT'
            }
        }
    }
}

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives javadocJar, sourcesJar
}

signing {
    sign configurations.archives
}

install {
    repositories.mavenInstaller {
        setPom jbakePom
    }
}
