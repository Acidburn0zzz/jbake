buildscript {
    repositories {
        jcenter()
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }

    dependencies {
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.7.3'
        classpath 'org.kt3k.gradle.plugin:coveralls-gradle-plugin:2.7.1'
        classpath "gradle.plugin.io.sdkman:gradle-sdkvendor-plugin:1.1.1"
    }
}

plugins {
    id "java"
    id "eclipse"
    id "idea"
    id "jacoco"
    id 'com.github.ben-manes.versions' version '0.13.0'
}

group = 'org.jbake'
version = '2.6.0-SNAPSHOT'

description = """jbake"""

if( JavaVersion.current().java7Compatible ) {
    apply plugin: 'com.github.kt3k.coveralls'
    apply plugin: 'com.jfrog.bintray'
    apply from: 'gradle/application.gradle'
    apply from: 'gradle/signing.gradle'
    apply from: 'gradle/maven-publishing.gradle'
    apply from: 'gradle/publishing.gradle'
    apply from: 'gradle/sdkman.gradle'
}

sourceCompatibility = 1.6
targetCompatibility = 1.6

repositories {
    jcenter()
}

ext {
    commonsIoVersion = '2.4'
    commonsConfigurationVersion = '1.9'
    jsonSimpleVersion = '1.1.1'
    args4jVersion = '2.0.26'
    freemarkerVersion = '2.3.20'
    orientDbVersion = '1.6.4'
    pegdownVersion = '1.4.2'
    asciidoctorjVersion = '1.5.2'
    jettyServerVersion = '8.1.12.v20130726'
    groovyVersion = '2.4.1'
    thymeleafVersion = '2.1.3.RELEASE'
    thymeleafExtrasVersion = '2.1.1.RELEASE'
    slf4jVersion = '1.7.6'
    logbackVersion = '1.1.1'
    jade4jVersion = '0.4.2'
    commonsVfs2Version = '2.0'
    junitVersion = '4.11'
    assertjCoreVersion = '1.7.0'
}

dependencies {
    compile group: 'commons-io', name: 'commons-io', version:  commonsIoVersion
    compile group: 'commons-configuration', name: 'commons-configuration', version: commonsConfigurationVersion
    compile group: 'com.googlecode.json-simple', name: 'json-simple', version: jsonSimpleVersion
    compile group: 'args4j', name: 'args4j', version: args4jVersion
    compile group: 'org.freemarker', name: 'freemarker', version: freemarkerVersion
    compile group: 'com.orientechnologies', name: 'orient-commons', version: orientDbVersion
    compile group: 'com.orientechnologies', name: 'orientdb-core', version: orientDbVersion
    compile group: 'org.pegdown', name: 'pegdown', version: pegdownVersion
    compile group: 'org.asciidoctor', name: 'asciidoctorj', version: asciidoctorjVersion
    compile group: 'org.eclipse.jetty', name: 'jetty-server', version: jettyServerVersion
    compile group: 'org.codehaus.groovy', name: 'groovy', version: groovyVersion
    compile group: 'org.codehaus.groovy', name: 'groovy-templates', version: groovyVersion
    compile group: 'org.thymeleaf', name: 'thymeleaf', version: thymeleafVersion
    compile group: 'org.thymeleaf.extras', name: 'thymeleaf-extras-conditionalcomments', version: thymeleafExtrasVersion
    compile group: 'org.slf4j', name: 'slf4j-api', version: slf4jVersion
    compile group: 'org.slf4j', name: 'jul-to-slf4j', version: slf4jVersion
    compile group: 'ch.qos.logback', name: 'logback-classic', version: logbackVersion
    compile group: 'ch.qos.logback', name: 'logback-core', version: logbackVersion
    compile group: 'de.neuland-bfi', name: 'jade4j', version: jade4jVersion
    compile group: 'org.apache.commons', name: 'commons-vfs2', version: commonsVfs2Version
    testCompile group: 'junit', name: 'junit', version: junitVersion
    testCompile group: 'org.assertj', name: 'assertj-core', version: assertjCoreVersion
}

jacoco {
    toolVersion = '0.7.7.201606060606'
}

jacocoTestReport {
    reports {
        xml.enabled = true // coveralls plugin depends on xml format report
        html.enabled = true
    }
}

jacocoTestReport.dependsOn test